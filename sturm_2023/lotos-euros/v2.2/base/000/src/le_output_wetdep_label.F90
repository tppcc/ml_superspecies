!###############################################################################
!
! NAME
!
!   LE_Output_WetDep_Label  -  LOTOS-EUROS output of labelled wet deposition fields
!
! HISTORY
!
!   2007 may, Arjo Segers, TNO
!
!
!###############################################################################
!
#define TRACEBACK write (gol,'("in ",a," (",a,", line",i5,")")') rname, __FILE__, __LINE__; call goErr
#define IF_NOTOK_RETURN(action) if (status/=0) then; TRACEBACK; action; return; end if
#define IF_ERROR_RETURN(action) if (status> 0) then; TRACEBACK; action; return; end if
!
#define IF_NF90_NOTOK_RETURN(action) if (status/=NF90_NOERR) then; gol=nf90_strerror(status); call goErr; TRACEBACK; action; return; end if
!
#include "le.inc"
!
!###############################################################################

module LE_Output_WetDep_Label

  use GO, only : gol, goPr, goErr
  use GO, only : TDate, TIncrDate

#ifdef with_netcdf
  use NetCDF, only : NF90_StrError, NF90_NOERR
#endif

  use LE_Output_Common, only : T_LE_Output_Common

  implicit none


  ! --- in/out -----------------------------

  private

  public  ::  T_LE_Output_WetDep_Label

  public  ::  LE_Output_WetDep_Label_Init, LE_Output_WetDep_Label_Done
  public  ::  LE_Output_WetDep_Label_PutOut


  ! --- const ------------------------------

  character(len=*), parameter   ::  mname = 'LE_Output_WetDep_Label'


  ! --- types ------------------------------

  type T_LE_Output_WetDep_Label
    ! name for this file:
    character(len=16)           ::  typ
    character(len=16)           ::  name
    ! common stuff:
    type(T_LE_Output_Common)    ::  com
    ! adhoc convention, might change per file ...
    character(len=32)           ::  convention
    ! replace existing files ?
    logical                     ::  replace
    ! file opened ?
    logical                     ::  opened
    ! pack output ?
    logical                     ::  pack_output
    ! current time range:
    type(TDate)                 ::  tr(2)
    ! time resolution:
    real                        ::  dhour
    ! collect: daily, instant
    character(len=32)           ::  collect
    ! time record counter:
    integer                     ::  itrec
    ! state name:
    character(len=16)           ::  state
    ! file name:
    character(len=256)          ::  fname
    ! file handle:
    integer                     ::  ncid
    ! dimension handles:
    integer                     ::  dimid_lon
    integer                     ::  dimid_lat
    integer                     ::  dimid_time
    integer                     ::  dimid_label
    integer                     ::  dimid_labelname_len
    ! dimension variables:
    integer                     ::  varid_lon
    integer                     ::  varid_lat
    integer                     ::  varid_time
    integer                     ::  varid_time_day
    integer                     ::  varid_time_dtg
    integer                     ::  varid_dh
    integer                     ::  varid_precip
    integer                     ::  varid_labelname
    ! tracer variables:
    integer                     ::  ntr
    integer, pointer            ::  itr(:)
    integer, pointer            ::  itr_glob(:)
    character(len=32), pointer  ::  name_tr(:)
    character(len=32), pointer  ::  unit_tr(:)
    real, pointer               ::  unitconv(:)
    integer, pointer            ::  varid_tr(:)
    ! grads ctl file ?
    logical                     ::  grads_ctl
    character(len=256)          ::  grads_ctl_file
    character(len=256)          ::  grads_ctl_base
    integer                     ::  grads_ctl_nt
    type(TDate)                 ::  grads_ctl_t1
    type(TIncrDate)             ::  grads_ctl_dt
    ! bounding box
    integer                     ::  i1, i2, ni
    integer                     ::  j1, j2, nj
    real                        ::  westb, southb
  
  end type T_LE_Output_WetDep_Label


contains


  ! ====================================================


  subroutine LE_Output_WetDep_Label_Init( leo, rcfile, rckey, typ, name, state, status )

    use GO     , only : TrcFile, Init, Done, ReadRc
    use GO     , only : goMatchValues
    use GO     , only : AnyDate

    use Dims   , only : nx, ny, nz, nspec
    use Dims   , only : nspec_all
    use Indices, only : specname, specunit
    use LE_Grid, only : ugg
    use LE_Output_Common, only : Init
    use LE_Data, only : LE_Data_Enable
    use SA_Labeling     , only : nwetdep_label_output_tracer
    use SA_Labeling     , only : labelled_specs_wetdep_output_names

    ! --- in/out --------------------------------

    type(T_LE_Output_WetDep_Label), intent(out)   ::  leo
    character(len=*), intent(in)          ::  rcfile
    character(len=*), intent(in)          ::  rckey
    character(len=*), intent(in)          ::  typ
    character(len=*), intent(in)          ::  name
    character(len=*), intent(in)          ::  state
    integer, intent(out)                  ::  status

    ! --- const ----------------------------

    character(len=*), parameter   ::  rname = mname//'/LE_Output_WetDep_Label_Init'

    ! --- local ---------------------------------

    type(TrcFile)         ::  rcF
    character(len=64)     ::  basekey
    character(len=256)    ::  tracer_names
    integer               ::  itr
    integer               ::  i
 
    character(len=512)    ::  key
    real                  ::  west, south, east, north
    real, pointer         ::  ff(:,:)

    ! --- begin ---------------------------------

    ! store name:
    leo%typ  = typ
    leo%name = name

    ! init common stuff:
    call Init( leo%com, rcfile, rckey, status )
    IF_NOTOK_RETURN(status=1)

    ! open rcfile:
    call Init( rcF, rcfile, status )
    IF_NOTOK_RETURN(status=1)

    ! replace existing files ?
    call ReadRc( rcF, trim(rckey)//'.replace', leo%replace, status )
    IF_NOTOK_RETURN(status=1)

    ! write GrADS ctl file ?
    call ReadRc( rcF, trim(rckey)//'.ctl', leo%grads_ctl, status )
    IF_NOTOK_RETURN(status=1)

    ! base key:
    write (basekey,'(a,".",a,".",a)') trim(rckey), trim(typ), trim(name)

    ! output convention:
    call ReadRc( rcF, trim(basekey)//'.convention', leo%convention, status )
    IF_NOTOK_RETURN(status=1)

    ! collect daily or instant
    call ReadRc( rcF, trim(basekey)//'.collect', leo%collect, status )
    IF_NOTOK_RETURN(status=1)

    ! output time resolution:
    call ReadRc( rcF, trim(basekey)//'.dhour', leo%dhour, status )
    IF_NOTOK_RETURN(status=1)

    ! pack output?
    call ReadRC( rcF, trim(basekey)//'.pack_output', leo%pack_output, status )
    IF_NOTOK_RETURN(status=1)

    ! tracer names:
    call ReadRc( rcF, trim(basekey)//'.fields', tracer_names, status )
    IF_NOTOK_RETURN(status=1)

    ! state name:
    leo%state = trim(state)

    ! setup storage for tracer fields:
    allocate( leo%itr     (nwetdep_label_output_tracer) )
    allocate( leo%itr_glob(nwetdep_label_output_tracer) )
    allocate( leo%name_tr (nwetdep_label_output_tracer) )
    allocate( leo%unit_tr (nwetdep_label_output_tracer) )
    allocate( leo%unitconv(nwetdep_label_output_tracer) )
    allocate( leo%varid_tr(nwetdep_label_output_tracer) )

    ! match tracer names:
    call goMatchValues( tracer_names, specname, &
                          leo%ntr, leo%name_tr, leo%itr_glob, &
                          status )
    IF_NOTOK_RETURN(status=1)
    ! match tracer names:
    call goMatchValues( tracer_names, labelled_specs_wetdep_output_names(1:nwetdep_label_output_tracer), &
                          leo%ntr, leo%name_tr, leo%itr, &
                          status )
    IF_NOTOK_RETURN(status=1)
    ! info ...
    write (gol,'("selected tracers for wetdep output:")'); call goPr
    do i = 1, leo%ntr
      itr = leo%itr_glob(i)
      write (gol,'("  ",i3,"  ",a10,"  (",i3, a10," ",a10,")")') &
                  i, leo%name_tr(i), &
                  itr, trim(specname(itr)), trim(specunit(itr)); call goPr
    end do

    ! files not open yet:
    leo%opened = .false.

    ! no time range set yet:
    leo%tr(1) = AnyDate()
    leo%tr(2) = AnyDate()

    ! init GrADS stuff:
    if ( leo%grads_ctl ) then
      ! no times written yet:
      leo%grads_ctl_nt = 0
    end if

    ! bounding box
    call ReadRc( rcF, trim(basekey)//'.bounding_box', key, status )
    IF_NOTOK_RETURN(status=1)
    ! empty?
    if (len_trim(key) == 0) then
      ! full domain
      leo%i1 = 1
      leo%i2 = nx
      leo%ni = nx
      leo%j1 = 1
      leo%j2 = ny
      leo%nj = ny
      leo%westb = ugg%longitude_bnds(1,1,1)
      leo%southb = ugg%latitude_bnds(1,1,1)
    else
      ! not yet ...
      write (gol,'("no output subset supported for domain decomposition yet")'); call goErr
      TRACEBACK; status=1; return
      
      select case ( trim(ugg%type) ) 
        
        case ( 'cartesian-regular') 
          ! read domain from key
          read(key,*,iostat=status) west, east, south, north
          if(status/=0) then
            write (gol,'("could not read domain from key: ",a)') trim(key); call goErr
            TRACEBACK; status=1; return
          endif

          ! Check if bounding box is in run domain
          if ( west < ugg%longitude_bnds_1d(1,1) .or. east > ugg%longitude_bnds_1d(2,ugg%nlon) .or. &
               south < ugg%latitude_bnds_1d(1,1) .or. north > ugg%latitude_bnds_1d(2,ugg%nlat) ) then
            write( gol, '("Bounding box domain is (partly) outside run domain")' ) ; call goErr
            write( gol, '("Run domain: ", 4f8.2)' ) ugg%longitude_bnds_1d(1,1),ugg%longitude_bnds_1d(2,ugg%nlon),ugg%latitude_bnds_1d(1,1),ugg%latitude_bnds_1d(2,ugg%nlat); call goErr
            write( gol, '("Bounding Box domain: ", 4f8.2)' ) west, east, south, north ; call goErr
            TRACEBACK;status=1;return
          endif

          ! for safety
          nullify(ff)
          ! get cell range covered by box
          call ugg%GetDistribution(west,east,south,north,leo%i1,leo%i2,leo%j1,leo%j2,ff,status)
          IF_NOTOK_RETURN(status=1)
          !clear, fractions not used
          if ( associated(ff) ) deallocate(ff)
          ! set shape
          leo%ni = leo%i2-leo%i1+1
          leo%nj = leo%j2-leo%j1+1
          ! set west/south bounds
          leo%westb  = ugg%longitude_bnds_1d(1,leo%i1)
          leo%southb = ugg%latitude_bnds_1d(1,leo%j1)
        case default 
          write( gol, '("Definition of bounding box not clear for grid-type: ", a)' ) trim(ugg%type) ; call goErr
          TRACEBACK;status=1;return
      end select
    end if

    ! close
    call Done( rcF, status )
    IF_NOTOK_RETURN(status=1)

    ! enable data:
    call LE_Data_Enable( 'dens', status )
    IF_NOTOK_RETURN(status=1)

    ! ok
    status = 0

  end subroutine LE_Output_WetDep_Label_Init


  ! ***


  subroutine LE_Output_WetDep_Label_Done( leo, status )

#ifdef with_netcdf
    use NetCDF          , only : NF90_Close
#endif
    use LE_Output_Common, only : Done

    ! --- in/out --------------------------------

    type(T_LE_Output_WetDep_Label), intent(inout)   ::  leo
    integer, intent(out)                    ::  status

    ! --- const ----------------------------

    character(len=*), parameter   ::  rname = mname//'/LE_Output_WetDep_Label_Done'

    character(len=256) :: commandline
    ! --- begin ---------------------------------

#ifndef with_netcdf
    ! check ...
    stop 'not compiled with netcdf support'
#endif

    ! file opened ?
    if ( leo%opened ) then
      ! close:
#ifdef with_netcdf
      status = NF90_Close( leo%ncid )
      IF_NF90_NOTOK_RETURN(status=1)

      ! pack output?
      if ( leo%pack_output ) then
        write( commandline, '(a," " a)' ) trim(leo%com%packscript), trim(leo%fname)
        call SYSTEM( trim(commandline) )
      end if
#endif
      ! reset flag:
      leo%opened = .false.
      ! write GrADS ctl file if necessary:
      call Write_GrADS_Ctl( leo, status )
      IF_NOTOK_RETURN(status=1)
    end if

    ! clear storage for tracer fields:
    deallocate( leo%itr      )
    deallocate( leo%itr_glob )
    deallocate( leo%name_tr  )
    deallocate( leo%unit_tr  )
    deallocate( leo%unitconv )
    deallocate( leo%varid_tr )

    ! done with common stuff ...
    call Done( leo%com, status )
    IF_NOTOK_RETURN(status=1)

    ! ok
    status = 0

  end subroutine LE_Output_WetDep_Label_Done


  ! ***


  subroutine LE_Output_WetDep_Label_PutOut( leo, t, status )

    use Binas  , only : xm_air
    use GO     , only : goc
    use GO     , only : TDate, TIncrDate, NewDate, IncrDate, Get
    use GO     , only : operator(+), operator(-), operator(<), operator(>), rTotal, dTotal, iTotal
    use GO     , only : wrtgol, Precisely, MidNight
    use LE_Grid, only : glb_ugg
    use C3PO   , only : T_Grid_NcDef

#ifdef with_netcdf
    use NetCDF , only : NF90_Create, NF90_Close
    use NetCDF , only : NF90_Def_Dim, NF90_Def_Var, NF90_EndDef
    use NetCDF , only : NF90_Put_Var, NF90_Put_Att
    use NetCDF , only : NF90_NOCLOBBER, NF90_CLOBBER, NF90_GLOBAL, NF90_UNLIMITED
    use NetCDF , only : NF90_REAL, NF90_INT, NF90_CHAR
#endif

    use LE_CF_Conventions   , only : LE_CF_names

    use Dims   , only : nx, ny, nspec
    use Indices, only : specname, specunit, specmolm
    use Indices, only : accum_n, accum_ii, accum_ww, accum_ppb_to_ugm3

    use LE_Output_Common, only : PutOut_GlobalAttributes
    use LE_Output_Tools , only : LE_Output_Define_Dims_Time
    use LE_Output_Tools , only : LE_Output_Define_Vars_Time
    use LE_Output_Tools , only : LE_Output_Put_Var_Time
    
    use LE_Output_Tools , only : LE_Output_Put_Var_Domains
    use LE_Data, only : LE_Data_GetPointer

    use SA_Labeling, only : SA_budget_wetdepos_hour
    use SA_Labeling, only : SA_nlabel
    use SA_Labeling, only : SA_Label_Names, labelname_len
    
    ! --- in/out --------------------------------

    type(T_LE_Output_WetDep_Label), intent(inout) ::  leo
    type(TDate), intent(in)                 ::  t
    integer, intent(out)                    ::  status

    ! --- const ---------------------------------

    character(len=*), parameter   ::  rname = mname//'/LE_Output_WetDep_Label_PutOut'

    ! --- local ---------------------------------

    integer               ::  time6(6)
    integer               ::  time
#ifdef with_netcdf
    integer               ::  cmode
#endif
    integer               ::  itr, itr_glob, iz
    integer               ::  SA_ilabel, labellen
    integer               ::  varid
    type(T_Grid_NcDef)    ::  gncd
    type(TDate)           ::  t0
    integer               ::  i, j
    integer               ::  l, la
    real, allocatable     ::  pat(:,:,:)
    real, allocatable     ::  convfact(:,:)

    character(len=256)    ::  cf_standard_name, cf_long_name, cf_units, cf_tracer_name
    character(len=512)    ::  comment
    character(len=32)     ::  varname
    character(len=32)     ::  afac
    
    character(len=256) :: commandline

    ! meteo data:
    real, pointer          ::  dens(:,:,:)   ! (lon,lat,nz) 
    real, pointer          ::  rain(:,:,:)   ! (lon,lat,1)    

    ! --- begin ----------------------------
    
    ! access meteo:
    call LE_Data_GetPointer( 'dens', dens, status, check_units ='kg/m3' )
    IF_NOTOK_RETURN(status=1)
    call LE_Data_GetPointer( 'rain', rain, status, check_units ='m/s' )
    IF_NOTOK_RETURN(status=1)

#ifndef with_netcdf
    ! check ...
    stop 'not compiled with netcdf support'
#endif

    ! for multiples of dhour only ...
    if ( .not. Precisely(t,leo%dhour,'hour')  ) then
      status=0; return
    end if
    
    !  only whole hours yet ...
    if ( leo%dhour /= int(leo%dhour) ) then
      write (gol,'("dhour should be integer : ",f8.4)') leo%dhour; call goErr
      TRACEBACK; status=1; return
    end if
    
    ! dummy ...
    cf_units = 'no-cf-units'

    ! current time not in time range ?
    if ( (t < leo%tr(1)) .or. (leo%tr(2) < t) ) then

      ! extract time fields:
      call Get( t, time6=time6 )

      ! daily or less ?
      select case ( trim(leo%collect) )
        ! collect daily for [00,24)
        case ( 'daily' )
          ! set time range [00,24) for this day:
          leo%tr(1) = NewDate( year=time6(1), month=time6(2), day=time6(3), hour=00 )
          leo%tr(2) = leo%tr(1) + IncrDate( day=1 ) - IncrDate(mili=1)
          ! new file name:
          write (leo%fname,'(a,"/",a,"_",a,"_",a,"_",i4.4,2i2.2)') &
                    trim(leo%com%outdir), trim(leo%com%model), trim(leo%com%expid), &
                    trim(leo%name), time6(1:3)
          if ( len_trim(leo%state) > 0 ) write (leo%fname,'(a,"_",a)') trim(leo%fname), trim(leo%state)
          write (leo%fname,'(a,".nc")') trim(leo%fname)
        ! collect daily for (00,24]
        case ( 'daily24' )
          ! set time range (00,24] for this day:
          if ( Midnight(t) ) then
            leo%tr(1) = t - IncrDate(day=1)
            leo%tr(2) = t
          else
            leo%tr(1) = NewDate( year=time6(1), month=time6(2), day=time6(3), hour=00 )
            leo%tr(2) = leo%tr(1) + IncrDate( day=1 )
            leo%tr(1) = leo%tr(1) + IncrDate(mili=1)
          end if
          ! new file name:
          write (leo%fname,'(a,"/",a,"_",a,"_",a,"_",i4.4,2i2.2)') &
                    trim(leo%com%outdir), trim(leo%com%model), trim(leo%com%expid), &
                    trim(leo%name), leo%tr(1)%year, leo%tr(1)%month, leo%tr(1)%day
          write (leo%fname,'(a,".nc")') trim(leo%fname)
        ! files with instant fields:
        case ( 'instant' )
          ! set time range for current instant time:
          leo%tr(1) = t
          leo%tr(2) = t
          ! new file name:
          write (leo%fname,'(a,"/",a,"_",a,"_",a,"_",i4.4,2i2.2,"_",2i2.2)') &
                    trim(leo%com%outdir), trim(leo%com%model), trim(leo%com%expid), &
                    trim(leo%name), time6(1:5)
          if ( len_trim(leo%state) > 0 ) write (leo%fname,'(a,"_",a)') trim(leo%fname), trim(leo%state)
          write (leo%fname,'(a,".nc")') trim(leo%fname)
        ! unknonw ...
        case default
          write (gol,'("unsupported collect key : ",a)') trim(leo%collect); call goErr
          TRACEBACK; status=1; return
      end select

      ! root only:
      if ( goc%root ) then

#ifdef with_netcdf
        ! set creation mode flag:
        if ( leo%replace ) then
          cmode = NF90_CLOBBER       ! overwrite existing files
        else
          cmode = NF90_NOCLOBBER     ! do not overwrite existing files
        end if

        ! create file:
        status = NF90_Create( trim(leo%fname), cmode, leo%ncid )
        if ( status /= NF90_NOERR ) then
          write (gol,'("creating file : ")'); call goErr
          write (gol,'("  file name  : ",a)') trim(leo%fname); call goErr
          write (gol,'("  nf90 error : ",a)') trim(nf90_strerror(status)); call goErr
          TRACEBACK; status=1; return
        end if
#else
        ! dummy ..
        leo%ncid = -1
#endif

        ! reset flag:
        leo%opened = .true.
  
        ! write global attributes:
        call PutOut_GlobalAttributes( leo%com, leo%ncid, status )
        IF_NOTOK_RETURN(status=1)

#ifdef with_netcdf
      
        ! grid dimensions/variables
        call glb_ugg%DefGrid_NetCDF( gncd, leo%ncid, status, &
                                    dimid_lon=leo%dimid_lon, dimid_lat=leo%dimid_lat )
                                    !subset=(/leo%i1,leo%i2,leo%j1,leo%j2/) )
        IF_NOTOK_RETURN(status=1)
      
        ! time dimensions
        call LE_Output_Define_Dims_Time(leo%ncid, leo%dimid_time, status)
        IF_NOTOK_RETURN(status=1)

        ! label dimension
        status = NF90_Def_Dim( leo%ncid, 'label', SA_nlabel, leo%dimid_label )
        IF_NF90_NOTOK_RETURN(status=1)
      
        ! len_labelname dimension
        status = NF90_Def_Dim( leo%ncid, 'labelname_len', labelname_len, leo%dimid_labelname_len )
        IF_NF90_NOTOK_RETURN(status=1)

        ! time since t0
        t0 = leo%com%t0      
        ! time variables
        call LE_Output_Define_Vars_Time(leo%ncid, leo%varid_time, leo%varid_time_dtg, &
                                        leo%dimid_time, trim(leo%com%CF_convention), t0, status)
        IF_NOTOK_RETURN(status=1)
      
        ! labelname variable
        status = NF90_Def_Var( leo%ncid, 'labelnames', NF90_CHAR, (/leo%dimid_labelname_len, leo%dimid_label/), varid )
        IF_NOTOK_RETURN(status=1)
        status = NF90_Put_Att( leo%ncid, varid, 'standard_name', 'Names of the label defnitions')
        IF_NOTOK_RETURN(status=1)
        leo%varid_labelname = varid      
#endif
      
      end if  ! root
      
      ! loop over tracers to be written:
      do l = 1, leo%ntr

        ! global tracer index
        itr = leo%itr(l)

        ! CF standard name for concentration/mixing ratio/column:

        ! no comment yet
        comment = ''

        ! set names etc following some convention:
        select case ( trim(leo%convention) )
        
          case ( 'cf' )

            ! standard variable name:
            varname = trim(leo%name_tr(l))//'_wflux'

            ! get names following CF conventions;
            ! store conversion factor for later usage:
            call LE_CF_names( &
                         specname(itr), specunit(itr), &
                         cf_standard_name, cf_long_name, cf_units, &
                         leo%unitconv(l), comment, &
                         status, cf_tracer_name=cf_tracer_name )
            IF_NOTOK_RETURN(status=1)
            
            ! reset to new name and unit ...
            cf_standard_name = 'tendency_of_atmosphere_mass_content_of_'//trim(cf_tracer_name)//'_due_to_wet_deposition'
            cf_long_name     = trim(cf_standard_name)
            cf_units = 'kg m-2 s-1'
            ! reset to dummy value, not used:
            leo%unitconv(l) = -999.9
        
#ifdef with_pollen
            ! special case for the pollen
            if ( varname == 'pol_b_wflux' .or. varname == 'pol_b' .or. &
                 varname == 'pol_g_wflux' .or. varname == 'pol_g' .or. &
                 varname == 'pol_o_wflux' .or. varname == 'pol_o' ) then
              cf_units = 'grns m-2 hr-1'
            end if
#endif            
            
          case ( 'megapoli' )

            ! standard variable name:
            varname = trim(leo%name_tr(l))//'_wflux'

            ! get names following CF conventions;
            ! store conversion factor for later usage:
            call LE_CF_names( &
                         specname(itr), specunit(itr), &
                         cf_standard_name, cf_long_name, cf_units, &
                         leo%unitconv(l), comment, &
                         status, cf_tracer_name=cf_tracer_name )
            IF_NOTOK_RETURN(status=1)
            
            ! reset to new name and unit ...
            cf_standard_name = 'wet_deposition_flux_of_'//trim(cf_tracer_name)
            cf_long_name     = trim(cf_standard_name)
            cf_units = 'ug m-2 h-1'
            ! reset to dummy value, not used:
            leo%unitconv(l) = -999.9
            
          case default
          
            write (gol,'("unsupported output convention : ",a)') trim(leo%convention); call goErr
            TRACEBACK; status=1; return
            
        end select
        
        ! store units for later usage (GrADS ctl file, conversions):
        leo%unit_tr(l) = trim(cf_units)

        ! accumulated specs ?
        if ( accum_n(itr) > 1 ) then
          if ( len_trim(comment) > 0 ) comment = trim(comment)//' ; '
          do la = 1, accum_n(itr)
            if ( la > 1 ) comment = trim(comment)//' + '
            if ( accum_ppb_to_ugm3(itr,la) ) then
              write (afac,'(f5.2)') accum_ww(itr,la)/xm_air
              comment = trim(comment)//trim(specname(accum_ii(itr,la)))//'*'//trim(afac)//'*dens'
            else
              write (afac,'(f5.2)') accum_ww(itr,la)
              comment = trim(comment)//trim(specname(accum_ii(itr,la)))//'*'//trim(afac)
            end if
          end do
        end if
        
        ! root?
        if ( goc%root ) then

#ifdef with_netcdf
          ! define variable:
          status = NF90_Def_Var( leo%ncid, trim(varname), NF90_REAL, &
                                   (/leo%dimid_lon,leo%dimid_lat,leo%dimid_label,leo%dimid_time/), varid )
          if (status/=0) then
            write (gol,'("defining variable : ",a)') trim(leo%name_tr(l)); call goErr
            TRACEBACK; status=1; return
          end if

          ! write attributes:
          status = nf90_put_att( leo%ncid, varid, 'standard_name', trim(cf_standard_name) )
          IF_NF90_NOTOK_RETURN(status=1)
          status = nf90_put_att( leo%ncid, varid, 'long_name', trim(cf_long_name) )
          IF_NF90_NOTOK_RETURN(status=1)
          status = nf90_put_att( leo%ncid, varid, 'units', trim(cf_units) )
          IF_NF90_NOTOK_RETURN(status=1)
          call glb_ugg%DefCoor_NetCDF( gncd, varid, status )
          IF_NOTOK_RETURN(status=1)
          ! molemass:
          status = nf90_put_att( leo%ncid, varid, 'molemass', specmolm(itr) )
          IF_NF90_NOTOK_RETURN(status=1)
          status = nf90_put_att( leo%ncid, varid, 'molemass_unit', 'kg mole-1' )
          IF_NF90_NOTOK_RETURN(status=1)
          ! optional comment:
          if ( len_trim(comment) > 0 ) then
            status = nf90_put_att( leo%ncid, varid, 'comment', trim(comment) )
            IF_NF90_NOTOK_RETURN(status=1)
          end if
#endif

          ! store variable id:
          leo%varid_tr(l) = varid
        end if  ! root
      end do  ! written tracers
      
      ! root?
      if ( goc%root ) then
        ! define variable rain
#ifdef with_netcdf
        ! define variable:
        status = NF90_Def_Var( leo%ncid, 'precip', NF90_REAL, &
                                 (/leo%dimid_lon,leo%dimid_lat,leo%dimid_time/), varid )
        if (status/=0) then
          write (gol,'("defining variable : ",a)') trim(leo%name_tr(l)); call goErr
          TRACEBACK; status=1; return
        end if

        ! write attributes:
        status = nf90_put_att( leo%ncid, varid, 'standard_name', 'Precipitation' )
        IF_NF90_NOTOK_RETURN(status=1)
        status = nf90_put_att( leo%ncid, varid, 'long_name', 'Precipitation' )
        IF_NF90_NOTOK_RETURN(status=1)
        status = nf90_put_att( leo%ncid, varid, 'units', 'liter m-2 s-1' )
        IF_NF90_NOTOK_RETURN(status=1)      
        call glb_ugg%DefCoor_NetCDF( gncd, varid, status )
        IF_NOTOK_RETURN(status=1)
        ! store variable id:
        leo%varid_precip = varid

        ! end defintion mode:
        status = NF90_EndDef( leo%ncid )
        IF_NF90_NOTOK_RETURN(status=1)
#endif      
      end if  ! root
      
      ! no records written yet:
      leo%itrec = 0

    end if

    ! next time record:
    leo%itrec = leo%itrec + 1
    
    ! root ?
    if ( goc%root ) then
      ! GrADS time counter:
      if ( leo%grads_ctl ) then
        ! increase counter:
        leo%grads_ctl_nt = leo%grads_ctl_nt + 1
        ! set times if necessary:
        if ( leo%grads_ctl_nt == 1 ) then
          leo%grads_ctl_t1 = t
          leo%grads_ctl_dt = IncrDate(day=1)   ! dummy ...
        end if
        if ( leo%grads_ctl_nt == 2 ) then
          leo%grads_ctl_dt = t - leo%grads_ctl_t1
        end if
      end if
    
      ! write dimension data only once ...
      if ( leo%itrec == 1 ) then
        ! write grid to netCDF file
        call glb_ugg%PutGrid_NetCDF( gncd, status )
        IF_NOTOK_RETURN(status=1)
        ! write labelnames
        do SA_ilabel = 1, SA_nlabel
          labellen = len(trim( SA_Label_Names(SA_ilabel)) )
          status = NF90_Put_Var(leo%ncid, leo%varid_labelname, trim(SA_Label_Names(SA_ilabel)), start=(/1,SA_ilabel/), count=(/labellen,1/) )
          IF_NF90_NOTOK_RETURN(status=1)
        end do
      end if  ! first record

      ! date up to seconds:
      call Get( t, time6=time6 )

      ! time since t0
      t0 = leo%com%t0      
      time = iTotal( t - t0, 'sec' )

      ! write time record:
#ifdef with_netcdf
      call LE_Output_Put_Var_Time(leo%ncid, leo%varid_time, leo%varid_time_dtg, &
                                   time, time6, trim(leo%com%CF_convention), leo%itrec, status )
      IF_NOTOK_RETURN(status=1)
#endif
    
    end if  ! root
    
    ! 2d field:
    allocate( pat(1:nx,1:ny,1:SA_nlabel) )
    allocate( convfact(1:nx,1:ny) )

    ! loop over all tracer to be written:
    do l = 1, leo%ntr
      ! global tracer index:
      itr = leo%itr(l)
      ! global tracer index:
      itr_glob = leo%itr_glob(l)
      ! init 2d field to zero:
      pat = 0.0
      ! add contributions; 
      pat(1:nx,1:ny,1:SA_nlabel) = SA_budget_wetdepos_hour(:,:,itr,:)

      ! unit conversion; 'cwet' is 'concentation * height', 
      ! thus 'ppb m' or 'ug/m3 m'
      select case ( trim(specunit(itr_glob))//' m -> '//trim(leo%unit_tr(l)) )
        !~ other notations ...
        case ( 'ug/m3 m -> ug m-2 h-1' )
          ! per hour:
          pat = pat / real(leo%dhour)  ! ug/m2/hr
        case ( 'ug/m3 m -> kg m-2 s-1' )
          ! kg per second:
          pat = pat * 1e-9 / real(leo%dhour*3600)  ! kg/m2/s
        !~ volume mixing ratio to mass concentration:
        case ( 'ppb m -> ug m-2 h-1' )
          ! In labeling routine already converted to ug/m2
          ! per hour:
          pat = pat / real(leo%dhour)  ! ug/m2/hr
        case ( 'ppb m -> kg m-2 s-1' )
          ! In labeling routine already converted to ug/m2
          ! kg per second:
          pat = pat * 1e-9 / real(leo%dhour*3600)  ! kg/m2/s

        !~ pollen concentrations
        case ( 'grns/m3 m -> grns m-2 hr-1' )
          ! per hour
          pat = pat / real(leo%dhour) ! #/m2/hr
        !~ unkown ...
        case default
          write (gol,'("do not know how to convert `",a,"` from `",a," m` to `",a,"`")') &
                  trim(specname(itr_glob)), trim(specunit(itr_glob)), trim(leo%unit_tr(l)); call goErr
          TRACEBACK; status=1; return
      end select

      ! write concentrations:
#ifdef with_netcdf
      ! write 2D field without level(-999):
      do SA_ilabel = 1, SA_nlabel
        call LE_Output_Put_Var_Domains( leo%ncid, leo%varid_tr(l), -999, leo%itrec, &
                                        pat(:,:,SA_ilabel), status, ilab=SA_ilabel )      
        IF_NOTOK_RETURN(status=1)
      end do
#endif
    end do  ! tracers

    ! write precipitation (note that mm is equivalent to liter/m2)
    ! mm/s            =     m/s     * mm/m 
    pat(1:nx,1:ny,1)  = rain(:,:,1) * 1.0e3   ! mm/s
#ifdef with_netcdf
    ! write 2D precipitation field without level(-999):
    call LE_Output_Put_Var_Domains( leo%ncid, leo%varid_precip, -999, leo%itrec, &
                                    pat(:,:,1), status )      
    IF_NOTOK_RETURN(status=1)
#endif

    ! clear:
    deallocate( pat )
    deallocate( convfact )
    
    ! root?
    if ( goc%root ) then      
      ! next time exceeds interval ?
      if ( t+IncrDate(hour=int(leo%dhour)) > leo%tr(2) ) then
#ifdef with_netcdf
        ! close:
        status = NF90_Close( leo%ncid )
        IF_NF90_NOTOK_RETURN(status=1)

        ! pack output?
        if ( leo%pack_output ) then
          write( commandline, '(a," " a)' ) trim(leo%com%packscript), trim(leo%fname)
          call SYSTEM( trim(commandline) )
        end if
#endif
        ! reset flag:
        leo%opened = .false.
        ! write GrADS ctl file if necessary:
        call Write_GrADS_Ctl( leo, status )
        IF_NOTOK_RETURN(status=1)
      end if
    end if  ! root?
    
    ! ok
    status = 0

  end subroutine LE_Output_WetDep_Label_PutOut


  ! ***


  subroutine Write_GrADS_Ctl( leo, status )

    use dims, only : nx, ny
    use GrADS_Ctl
    use SA_Labeling, only : SA_nlabel
    use SA_Labeling, only : SA_Label_Names, SA_Short_Label_Names
    use LE_Grid, only : glb_ugg

    ! --- in/out ---------------------------------

    type(T_LE_Output_WetDep_Label), intent(inout)   ::  leo
    integer, intent(out)                    ::  status

    ! --- const ---------------------------------

    character(len=*), parameter   ::  rname = mname//'/Write_GrADS_Ctl'

    ! --- local ----------------------------------

    type(T_GrADS_Ctl)     ::  ctl
    integer               ::  l
    character(len=512)    ::  line
    integer               ::  SA_ilabel
    integer               ::  Nvars
    character(len=512)    ::  dim_descr
    character(len=512)    ::  comment_line

    ! --- begin ----------------------------------

    ! write ctl file ?
    if ( leo%grads_ctl ) then

      ! ctl file name:
      write (leo%grads_ctl_file,'(a,"_",a,"_",a)') &
                trim(leo%com%model), trim(leo%com%expid), trim(leo%name)
      if ( len_trim(leo%state) > 0 ) write (leo%grads_ctl_file,'(a,"_",a)') trim(leo%grads_ctl_file), trim(leo%state)
      write (leo%grads_ctl_file,'(a,".ctl")') trim(leo%grads_ctl_file)

      ! daily or less ?
      select case ( trim(leo%collect) )
        ! collect daily for [00,24)
        case ( 'daily', 'daily24' )
          ! template for data files:
          write (leo%grads_ctl_base,'("^",a,"_",a,"_",a,"_%y4%m2%d2")') &
                    trim(leo%com%model), trim(leo%com%expid), trim(leo%name)
          if ( len_trim(leo%state) > 0 ) write (leo%grads_ctl_base,'(a,"_",a)') trim(leo%grads_ctl_base), trim(leo%state)
          write (leo%grads_ctl_base,'(a,".nc")') trim(leo%grads_ctl_base)
        ! files with instant fields:
        case ( 'instant' )
          ! template for data files:
          write (leo%grads_ctl_base,'("^",a,"_",a,"_",a,"_%y4%m2%d2_%h2%n2")') &
                    trim(leo%com%model), trim(leo%com%expid), trim(leo%name)
          if ( len_trim(leo%state) > 0 ) write (leo%grads_ctl_base,'(a,"_",a)') trim(leo%grads_ctl_base), trim(leo%state)
          write (leo%grads_ctl_base,'(a,".nc")') trim(leo%grads_ctl_base)
        ! unknonw ...
        case default
          write (gol,'("unsupported collect key : ",a)') trim(leo%collect); call goErr
          TRACEBACK; status=1; return
      end select

      ! open ctl file:
      call GrADS_Ctl_Init( ctl, trim(leo%com%outdir), trim(leo%grads_ctl_file), status )
      IF_NOTOK_RETURN(status=1)
      ! comment ...
      call GrADS_Ctl_Comment( ctl, '', status )
      call GrADS_Ctl_Comment( ctl, 'GrADS Data Descriptor File', status )
      call GrADS_Ctl_Comment( ctl, '', status )
      call GrADS_Ctl_Comment( ctl, 'Label definitions', status )
      call GrADS_Ctl_Comment( ctl, '', status )
      do SA_ilabel = 1, SA_nlabel
        write( comment_line , '(a," : ", a)' ) trim(SA_Short_Label_names(SA_ilabel)), trim(SA_Label_Names(SA_ilabel))
        call GrADS_Ctl_Comment( ctl, trim(comment_line) , status )
      end do
      call GrADS_Ctl_Comment( ctl, '', status )
      ! data set:
      call GrADS_Ctl_DSet( ctl, trim(leo%grads_ctl_base), status )
      IF_NOTOK_RETURN(status=1)
      ! title:
      write (line,'("model: ",a,"; expid: ",a)') trim(leo%com%model), trim(leo%com%expid)
      call GrADS_Ctl_Title( ctl, trim(line), status )
      IF_NOTOK_RETURN(status=1)
      ! write xdef/ydef from grid/projection definition
      call glb_ugg%WriteCtlProjection( ctl, status)
      IF_NOTOK_RETURN(status=1)
      ! zdef:
      call GrADS_Ctl_ZDef( ctl, (/0/), status )
      IF_NOTOK_RETURN(status=1)
      ! tdef:
      call GrADS_Ctl_TDef( ctl, leo%grads_ctl_nt, &
                               (/leo%grads_ctl_t1%year,leo%grads_ctl_t1%month,leo%grads_ctl_t1%day,leo%grads_ctl_t1%hour,leo%grads_ctl_t1%min/), &
                               (/                    0,                     0,leo%grads_ctl_dt%day,leo%grads_ctl_dt%hour,leo%grads_ctl_dt%min/), &
                               status )
      IF_NOTOK_RETURN(status=1)
      ! number of variables in ctl file, number of variables x number of labels
      Nvars = leo%ntr * SA_nlabel      
      call GrADS_Ctl_Vars( ctl, Nvars, status )
      ! loop over tracers to be written:
      do l = 1, leo%ntr
        do SA_ilabel = 1, SA_nlabel
          ! set variable description:
          write (line,'(a"_wflux [",a,"]")') trim(leo%name_tr(l)), trim(leo%unit_tr(l))
          ! set dimension description t, labelnr, z,y,x
          write (dim_descr, '("t, ",i2,",y,x")') SA_ilabel-1  
          ! add variable description:
          call GrADS_Ctl_Var( ctl, trim(leo%name_tr(l))//'_wflux', 0, trim(dim_descr), trim(line), status, label_name = SA_Short_Label_Names(SA_ilabel) )
          IF_NOTOK_RETURN(status=1)
        end do
      end do
      ! end of variables section:
      call GrADS_Ctl_EndVars( ctl, status )
      IF_NOTOK_RETURN(status=1)
      ! close ctl file:
      call GrADS_Ctl_Done( ctl, status )
      IF_NOTOK_RETURN(status=1)

    end if

    ! ok
    status = 0

  end subroutine Write_GrADS_Ctl


end module LE_Output_WetDep_Label
